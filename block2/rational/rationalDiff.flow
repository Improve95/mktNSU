import labs/block2/rational/rational;



export {
    diffRe(ex : RatExpr, var : Var) -> RatExpr;
    re2simpleRe(e : RatExpr) -> RatExpr;
}

diffRe(ex : RatExpr, var : Var) -> RatExpr {
    switch (ex) {
        Mul(a, b) : 
            re2simpleRe( Sum(
                re2simpleRe( Mul(diffRe(a, var), b) ), 
                re2simpleRe( Mul(a, diffRe(b, var)) ))
            );
        Sum(a, b) : 
            re2simpleRe( Sum(
                diffRe(a, var), 
                diffRe(b, var))
            );
        Sub(a, b) : 
            re2simpleRe( Sub(
                diffRe(a, var), 
                diffRe(b, var))
            );
        Div(a, b) : 
            re2simpleRe( Div(
                re2simpleRe( Sub(
                    re2simpleRe( Mul(diffRe(a, var), b) ), 
                    re2simpleRe( Mul(a, diffRe(b, var))) ) ), 
                re2simpleRe( Mul(b, b) ))
            );
        Var(a) : 
            if (a == var.var) Const(1)
            else Const(0);
        Const(a) : 
            Const(0);
        Neg(a) : 
            re2simpleRe( Neg(diffRe(a, var)) );
    }
}

re2simpleRe(e : RatExpr) -> RatExpr {
    switch(e) {
        Mul(l, r) : {
            simpleLeft = re2simpleRe(l);
            simpleRight = re2simpleRe(r);
            if (simpleLeft == Const(1)) {
                simpleRight;
            } else if (simpleRight == Const(1)) {
                simpleLeft;
            } else if (simpleLeft == Const(0) || simpleRight == Const(0)) {
                Const(0);
            } else {
                Mul(simpleLeft, simpleRight);
            }
        }
        Div(l, r) : {
            simpleLeft = re2simpleRe(l);
            simpleRight = re2simpleRe(r);
            if (simpleLeft == Const(0)) {
                Const(0);
            } else if (simpleRight == Const(1)) {
                simpleLeft;
            } else if (simpleLeft == simpleRight) {
                Const(1);
            } else {
                Div(simpleLeft, simpleRight);
            }
        }
        Sum(l, r) : {
            simpleLeft = re2simpleRe(l);
            simpleRight = re2simpleRe(r);
            if (simpleLeft == Const(0)) {
                simpleRight;
            } else if (simpleRight == Const(0)) {
                simpleLeft;
            } else {
                Sum(simpleLeft, simpleRight);
            }
        }
        Sub(l, r) : {
            simpleLeft = re2simpleRe(l);
            simpleRight = re2simpleRe(r);
            if (simpleLeft == Const(0)) {
                Neg(simpleRight);
            } else if (simpleRight == Const(0)) {
                simpleLeft;
            } else if (simpleLeft == simpleRight) {
                Const(0);
            } else {
                Sub(simpleLeft, simpleRight);
            }
        }
        default : e;
    }
}
